#!/usr/bin/perl

# convert the output from blastSimilarity to a format compatible 
# with bulk loading (sqlldr) into the ApiDB.SimilarSequences table

# assumes that the id field is in this format:
# aa_sequence_id-taxon_id

#>371323 (115 subjects)
#  Sum: 371323:533:e-150:1:277:1:277:1:277:277:277:0:

# the fields in the Sum line are:
#subject_Identifier:score:pvalue:minSubjectStart:maxSubjectEnd:minQueryStart:maxQueryEnd:numberOfMatches:totalMatchLength:numberIdentical:numberPositive:isReversed:readingFrame:+3fields related to percent match
use strict;

my $query_id;
my $query_taxon;

print "LOAD DATA
INFILE *
INTO TABLE ApiDB.SimilarSequences
FIELDS TERMINATED BY ', '
(query_id, subject_id, query_taxon_id, subject_taxon_id, evalue_mant, evalue_exp, percent_identity, percent_match)
BEGINDATA
";

while (<>) {

  # if new query
  if (/^\>(\d+)\-(\d+)/) {
    $query_id = $1;
    $query_taxon = $2;
  }

  elsif (/Sum: (.*)/) {
    my $stuff = $1;
    my @fields = split(/\:/, $stuff);
    my ($subject_id, $subject_taxon) = split(/\-/,$fields[0]);
    $fields[2] = '1' . $fields[2] if ($fields[2] =~ /^e/);
    my ($evalue_mant, $evalue_exp) = split(/e\-/, $fields[2]);
    ($evalue_mant, $evalue_exp) = (0,0) if $fields[2] eq '0.';
    my ($totalMatchLength, $numberIdentical) = ($fields[8], $fields[9]);
    my $percentIdent = int($numberIdentical / $totalMatchLength * 100 + .5);
    my $percentMatch = int($fields[15] +.5);
    print "$query_id, $subject_id, $query_taxon, $subject_taxon, $evalue_mant, $evalue_exp, $percentIdent, $percentMatch\n";
  }
}
