#!/usr/bin/perl

use strict;
use lib "$ENV{GUS_HOME}/lib/perl";
use Bio::SeqIO;
use Getopt::Long;

use Data::Dumper;
use FileHandle;
use HTTP::Date;

use DBI;
use DBD::Oracle;
use CBIL::Util::PropertySet;


my ($date, ) = split(" ", HTTP::Date::time2iso());
$date = join("",split(/-/,$date));

my ($verbose, $help, $organismAbbrev, $extDbName, $extDbRlsVer, $optionalTaxonId, $optionalFunctAnnot, $project);

&GetOptions('help|h' => \$help,
            'organismAbbrev=s' => \$organismAbbrev,
            'extDbName=s' => \$extDbName,
            'extDbRlsVer=s' => \$extDbRlsVer,
            'optionalTaxonId=s' => \$optionalTaxonId,
            'optionalFunctAnnot=s' => \$optionalFunctAnnot,
            'project=s' => \$project,
           );

&usage() if($help);

&usage("Missing a Required Argument") unless (defined $organismAbbrev);
&usage("Missing Required Argument") unless (defined ($extDbName && $extDbRlsVer) );

my $gusConfigFile = "$ENV{GUS_HOME}/config/gus.config";
my @properties = ();
my $gusconfig = CBIL::Util::PropertySet->new($gusConfigFile, \@properties, 1);
my $u = $gusconfig->{props}->{databaseLogin}; 
my $pw = $gusconfig->{props}->{databasePassword}; 
my $dsn = $gusconfig->{props}->{dbiDsn}; 
my $dbh = DBI->connect($dsn, $u, $pw) or die DBI::errstr;
$dbh->{RaiseError} = 1;
$dbh->{LongTruncOk} = 1;
$dbh->{LongReadLen} = 512 * 512 * 1024;

## get the extDbRlsId
my $extDbRlsId;
my $sqlExtDbRlsId = "
select edr.external_database_release_id from sres.externaldatabase ed, sres.externaldatabaserelease edr 
where ed.external_database_id=edr.external_database_id and ed.name like '$extDbName' and edr.version='$extDbRlsVer'
";
my $extDbRlsIdRef = readFromDatabase($dbh, $sqlExtDbRlsId);
$extDbRlsId = $extDbRlsIdRef->[0];
print STDERR "For $organismAbbrev, get extDbRlsId=$extDbRlsId\n";

## get the sequence info
my (%sSequences, %sChrs, %sChrOrders, %taxonIds, %sLengths, %naSeqIds);
my $sqlSeqsInfo = "select na_sequence_id, taxon_id, source_id, chromosome, chromosome_order_num, length, sequence
 from DOTS.externalnasequence where external_database_release_id=$extDbRlsId
";
my $seqsInfoRef = readFromDatabase($dbh, $sqlSeqsInfo);
foreach my $i (0..$#$seqsInfoRef) {
  my ($naSeqId, $taxonId, $sSeqId, $sChr, $sChrOrder, $sLength, $sSeq) = split(/\t/, $seqsInfoRef->[$i]);
  if ($naSeqId && $sSeqId && $sSeq) {
    $sSequences{$sSeqId} = $sSeq;
    $sChrs{$sSeqId} = $sChr;
    $sChrOrders{$sSeqId} = $sChrOrder;
    $taxonIds{$sSeqId} = $taxonId;
    $sLengths{$sSeqId} = $sLength;
    $naSeqIds{$sSeqId} = $naSeqId;
  }
}

## get product info
my $productRef = getProductNames ($dbh, $extDbRlsId);

## which functional annotation that want to output
my (%functAnnot, $geneNameRef, $geneSynonymRef, $transECRef, $transGORef, $transNoteRef, $genePmidRef);

my @functAnnots = split(/\,/, $optionalFunctAnnot);
foreach my $i (0..$#functAnnots) {
  if ($functAnnots[$i] =~ /genename/i) {
    $functAnnot{'geneName'} = 1;
    $geneNameRef = getGeneNames ($dbh, $extDbRlsId);
  } elsif ($functAnnots[$i] =~ /synonym/i) {
    $functAnnot{'synonym'} =1;
    $geneSynonymRef = getGeneSynonyms ($dbh, $extDbRlsId);
  } elsif ($functAnnots[$i] =~ /ec/i) {
    $functAnnot{'EC'} =1;
    $transECRef = getTransECs ($dbh, $extDbRlsId);
  } elsif ($functAnnots[$i] =~ /go/i) {
    $functAnnot{'GO'} =1;
    $transGORef = getTransGOs ($dbh, $organismAbbrev, $extDbRlsId);
  } elsif ($functAnnots[$i] =~ /note/i) {
    $functAnnot{'note'} =1;
    $transNoteRef = getTransNotes ($dbh, $extDbRlsId);
  } elsif ($functAnnots[$i] =~ /pmid/i) {
    $functAnnot{'pmid'} =1;
    $genePmidRef = getGeneAndTransPmids ($dbh, $extDbRlsId);
  }
}


## for each sequence
foreach my $k (sort keys %naSeqIds) {
#  if ($k eq "KB909916") {

  ## print the sequence file
  my $faFile = $k.".fsa";
  open (FA, ">$faFile") || die "can not open $faFile to write\n";
  my $defline = ">$k";
  my $sqlOrganism = "select t.ncbi_tax_id, tn.name from SRES.taxon t, SRES.taxonname tn 
where t.taxon_id=tn.taxon_id and t.taxon_id=$taxonIds{$k}";
  my $organismRef = readFromDatabase($dbh, $sqlOrganism);
  my ($ncbiTaxId, $organism) = split(/\t/, $organismRef->[0]);
  $ncbiTaxId = $optionalTaxonId if ($optionalTaxonId);

  $defline .= " [organism=$organism]" if ($organism);
  $defline .= " [chromosome=$sChrs{$k}]" if ($sChrs{$k});
  $defline .= " \n";
  print FA "$defline";
  print FA "$sSequences{$k}\n";
  close FA;


  ## print the tbl file
  my $tblFile = $k.".tbl";
  open (my $ofh, ">$tblFile") || die "can not open $tblFile to write\n";
  print $ofh "\>Feature\t$k\n";

  ## print source -- this is optional
  print $ofh "1\t$sLengths{$k}\tsource\t\t\n";
  printGenbankTagInfo ($ofh, "organism", $organism);
  printGenbankTagInfo ($ofh, "db_xref", "taxon:$ncbiTaxId");

  ## if genbank submission, need grep more info here, such as 

  ## get the genes in the sequence
  my $sqlGene = "select gf.source_id, gf.na_feature_id, gf.name, nl.start_min, nl.end_max, nl.is_reversed
from dots.genefeature gf, DOTS.nalocation nl
where gf.na_feature_id=nl.na_feature_id and gf.na_sequence_id=$naSeqIds{$k}
and gf.external_database_release_id=$extDbRlsId order by nl.start_min
";

  my $geneRef = readFromDatabase($dbh, $sqlGene);
  foreach my $j (0..$#$geneRef) {
    my ($gSourceId, $gNaFeatureId, $gType, $gStart, $gEnd, $gStrand) = split(/\t/, $geneRef->[$j]);
    $gType =~ s/\_gene//g;
    $gType = "mRNA" if ($gType !~ /RNA/i);   ## in the case of coding_gene or pseudo_gene

    ## FOR GENE
    ## get if partial info
    my (%prefix5, %prefix3);
    if ($gType eq 'mRNA') {

      ## get the spliced na sequence
      my $sqlSplicedNaSeq = "select t.SOURCE_ID, t.na_feature_id, sns.sequence 
from dots.transcript t, DOTS.splicednasequence sns 
where t.na_sequence_id = sns.NA_SEQUENCE_ID and t.PARENT_ID=$gNaFeatureId
";
      my $splicedNaSeqRef = readFromDatabase($dbh, $sqlSplicedNaSeq);

      foreach my $j (0..$#$splicedNaSeqRef) {
	my ($tSourceId, $tNaFeatureId, $splicedNaSeq) = split (/\t/, $splicedNaSeqRef->[$j]);

	## get translation_start and translation_end
	my $sqlTranslatLoc = "select taf.translation_start, taf.translation_stop 
from DOTS.translatedaafeature taf, dots.transcript t 
where taf.na_feature_id = t.NA_FEATURE_ID and t.NA_FEATURE_ID=$tNaFeatureId
";

	my $translatLocRef = readFromDatabase($dbh, $sqlTranslatLoc);
	my ($trsltStart, $trsltEnd) = split(/\t/, $translatLocRef->[0]);

	## the sequences in dots.splicednasequence, the reversed strands have been upcased and reversed & complement
	## so do not need to deal with strand here
	my @codons;
	for (my $j=$trsltStart-1; $j<$trsltEnd-1; $j+=3) {
	  push @codons, substr($splicedNaSeq, $j, 3);
	}
	my $first_codon = shift(@codons);
	$prefix5{$gNaFeatureId} = "<" if ($first_codon ne 'ATG');
	$prefix5{$tNaFeatureId} = "<" if ($first_codon ne 'ATG');

	my $last_codon = pop @codons;
	$prefix3{$gNaFeatureId} = ">" if ( !($last_codon eq 'TAA' || $last_codon eq 'TAG' || $last_codon eq 'TGA') );
	$prefix3{$tNaFeatureId} = ">" if ( !($last_codon eq 'TAA' || $last_codon eq 'TAG' || $last_codon eq 'TGA') );

	#print STDERR "$gSourceId\t$first_codon\t$last_codon\n" if ($gSourceId eq 'EAH_00051170');
      }
    }

    ## print gene
    ($gStart, $gEnd) = addLocPrefix($gStrand, $gStart, $gEnd, $prefix5{$gNaFeatureId}, $prefix3{$gNaFeatureId});

    ($gStrand != 1) ? print $ofh "$gStart\t$gEnd\tgene\t\t\n" : print $ofh "$gEnd\t$gStart\tgene\t\t\n";
    printGenbankTagInfo ($ofh, "locus_tag", "$gSourceId");

    ## print functional annotation data
    printGeneNameTbl ($ofh, $gSourceId, $geneNameRef) if ($functAnnot{'geneName'});
    printSynonymTbl ($ofh, $gSourceId, $geneSynonymRef) if ($functAnnot{'synonym'});

    ## print a dbxref link to EuPathDB
    #printDbxRef2EuPathDB ($ofh, $gSourceId, $project);  ## comment out for now until people request it

    ## FOR TRANSCRIPT
    ## get the codon_start
    my $codonStart = 1;
    my (@cdsPairs, $cdsPairsString);

    my $sqlTranscriptRef = "select na_feature_id, source_id, is_pseudo, TRANSL_TABLE from dots.transcript where PARENT_ID=$gNaFeatureId";
    my $transcriptRef = readFromDatabase($dbh, $sqlTranscriptRef);
    foreach my $m (0..$#$transcriptRef) {
      my ($tNaFeatureId, $tSourceId, $tIsPseudo, $tTranslTable) = split(/\t/, $transcriptRef->[$m]);

      ## get the transcript position from coding_start and coding_end in dots.aaFeatureExon table
      my $sqlExonLocRef;
      if ($gType eq "mRNA") {
	$sqlExonLocRef = "select rfe.ORDER_NUMBER, nl.START_MIN, nl.END_MAX, afe.CODING_START, afe.CODING_END
from DOTS.TRANSCRIPT t, DOTS.RNAFEATUREEXON rfe, DOTS.EXONFEATURE ef, DOTS.AAFEATUREEXON afe, DOTS.TRANSLATEDAAFEATURE taf, DOTS.NALOCATION nl
where t.NA_FEATURE_ID=rfe.RNA_FEATURE_ID and rfe.EXON_FEATURE_ID=ef.NA_FEATURE_ID and ef.NA_FEATURE_ID=nl.NA_FEATURE_ID
and t.NA_FEATURE_ID=taf.NA_FEATURE_ID and taf.AA_FEATURE_ID=afe.AA_FEATURE_ID and afe.EXON_FEATURE_ID=ef.NA_FEATURE_ID
and t.NA_FEATURE_ID=$tNaFeatureId order by rfe.ORDER_NUMBER";
      } else {
	$sqlExonLocRef = "select rfe.ORDER_NUMBER, nl.START_MIN, nl.END_MAX
from DOTS.TRANSCRIPT t, DOTS.RNAFEATUREEXON rfe, DOTS.EXONFEATURE ef, DOTS.NALOCATION nl
where t.NA_FEATURE_ID=rfe.RNA_FEATURE_ID and rfe.EXON_FEATURE_ID=ef.NA_FEATURE_ID and ef.NA_FEATURE_ID=nl.NA_FEATURE_ID
and t.NA_FEATURE_ID=$tNaFeatureId order by rfe.ORDER_NUMBER";
      }

      my $exonLocRef = readFromDatabase($dbh, $sqlExonLocRef);
      my $cdsCtr = 0;
      foreach my $i (0..$#$exonLocRef) {
	my ($order, $tStart, $tEnd, $cStart, $cEnd) = split(/\t/, $exonLocRef->[$i]);

	## deal with codonStart and CDS
	if ($cStart && $cEnd) {
	  my $shift = 0;
	  if ($gStrand == 0) {
	    $shift = abs($tStart - $cStart);
	  } else {
	    $shift = abs($tEnd - $cStart);
	  }
	  if ($shift > 0 && $shift < 3 && $cdsCtr == 0 && $prefix5{$tNaFeatureId}) {
	    $codonStart += $shift;
	    $cStart = ($gStrand == 0) ? ($cStart - $shift) : ($cStart + $shift);
	  }
	  $cdsPairsString = "$order\t$cStart\t$cEnd";
	  push (@cdsPairs, $cdsPairsString);
	  $cdsCtr++;
	  #print "after, $tStart, $tEnd, $cStart, $cEnd, $shift, $codonStart\n" if ($gNaFeatureId eq "45637516" || $gNaFeatureId eq "45647687");
	}

	## need to add start and end seperately
	($tStart, $tEnd) = addLocPrefix($gStrand, $tStart, $tEnd, $prefix5{$tNaFeatureId}, '') if ($i == 0 && $prefix5{$tNaFeatureId});
	($tStart, $tEnd) = addLocPrefix($gStrand, $tStart, $tEnd, '', $prefix3{$tNaFeatureId}) if ($i == $#$exonLocRef && $prefix3{$tNaFeatureId});

	## print transcript location
	if ($i == 0) {
	  ($gStrand != 1) ? print $ofh "$tStart\t$tEnd\t$gType\t\t\n" : print $ofh "$tEnd\t$tStart\t$gType\t\t\n";
	} else {
	  ($gStrand != 1) ? print $ofh "$tStart\t$tEnd\t\t\t\n" : print $ofh "$tEnd\t$tStart\t\t\t\n";
	}

	## print transcript info 
	if ($i == $#$exonLocRef) {
	  printGenbankTagInfo ($ofh, "locus_tag", "$gSourceId") if ($i == $#$exonLocRef);

	  ## TODO  ncRNA_class
	  my $ncRNA_class;
	  if ($gType =~ /ncRNA/) {
	    ($ncRNA_class) ? printGenbankTagInfo ($ofh, "ncRNA_class", $ncRNA_class)
	      : printGenbankTagInfo ($ofh, "ncRNA_class", "other");
	  }
	}
      }

      if ($gType =~ /mRNA/) {

	## print CDS
	foreach my $i (0..$#cdsPairs) {
	  my ($order, $cStart, $cEnd) = split(/\t/, $cdsPairs[$i]);
	  $cStart = $prefix5{$tNaFeatureId}.$cStart if ($i == 0 && $prefix5{$tNaFeatureId});
	  $cEnd = $prefix3{$tNaFeatureId}.$cEnd if ($i == $#cdsPairs && $prefix3{$tNaFeatureId});
	  ($i == 0) ? print $ofh "$cStart\t$cEnd\tCDS\t\t\n" : print $ofh "$cStart\t$cEnd\t\t\t\n";
	}
	#print $ofh "\t\t\tlocus_tag\t$gSourceId\n";
	printGenbankTagInfo ($ofh, "locus_tag", "$gSourceId");

	if ($tIsPseudo != 1) {
	  ## print product info for CDS
	  printProductTbl ($ofh, $tSourceId, $productRef);

	  ## get transl_table info
	  #print $ofh "\t\t\ttransl_table\t$tTranslTable\n" if ($tTranslTable);  ## only print transl_table if it is not null
	  printGenbankTagInfo ($ofh, "transl_table", $tTranslTable) if ($tTranslTable);  ## only print transl_table if it is not null

	  ## need to print condon_start if $prefix5
	  #print $ofh "\t\t\tcodon_start\t$codonStart\n" if ($prefix5{$tNaFeatureId});
	  printGenbankTagInfo ($ofh, "codon_start", $codonStart) if ($prefix5{$tNaFeatureId});

	  ## print a dbxref link to EuPathDB
	  #printDbxRef2EuPathDB ($ofh, $tSourceId, $project);

	  ## print functional annotation data for transcript
	  printEcTbl ($ofh, $tSourceId, $transECRef) if ($functAnnot{'EC'});
	  printGoTbl ($ofh, $tSourceId, $transGORef) if ($functAnnot{'GO'});
	  printPmidTbl ($ofh, $gSourceId, $tSourceId, $genePmidRef) if ($functAnnot{'pmid'});
	  printNoteTbl ($ofh, $tSourceId, $transNoteRef) if ($functAnnot{'note'});

	  ## add protein sequence if mRNA
	  my $sqlProteinSeq = "select sequence from DOTS.translatedaasequence where aa_sequence_id in
 (select aa_sequence_id from DOTS.translatedaafeature where na_feature_id in
 (select na_feature_id from DOTS.transcript where parent_id=$gNaFeatureId ) )";
	  my $proteinSeqRef = readFromDatabase($dbh, $sqlProteinSeq);

	  ## only take the 1st translatedaaseqeunce 
	  ## TODO -- find an example in genbank file that have more than one translation for one transcript
	  my $proteinSeq = $proteinSeqRef->[0];
	  ($proteinSeq) ? printGenbankTagInfo ($ofh, "translation", $proteinSeq)
	    : print STDERR "ERROR: for $gNaFeatureId, can not find protein seq\n";

	}

      } else {

	## print exon if not mRNA, maybe this is unnecessary
	foreach my $m (0..$#$exonLocRef) {
	  my ($eOrder, $eStart, $eEnd) = split(/\t/, $exonLocRef->[$m]);
	  $eStart = $prefix5{$tNaFeatureId}.$eStart if ($m == 0);
	  $eEnd = $prefix3{$tNaFeatureId}.$eEnd if ($m == $#$exonLocRef);
	  if ($m == 0) {
	    ($gStrand != 1) ? print $ofh "$eStart\t$eEnd\texon\t\t\n" : print $ofh "$eEnd\t$eStart\texon\t\t\n";
	  }else{
	    ($gStrand != 1) ? print $ofh "$eStart\t$eEnd\t\t\t\n" : print $ofh "$eEnd\t$eStart\t\t\t\n";
	  }
	  print STDERR "For $gSourceId, \$eStart= $eStart, \$eEnd = $eEnd\n" if ($gSourceId eq "NERG_03011");
	}

	#print $ofh "\t\t\tlocus_tag\t$gSourceId\n";
	printGenbankTagInfo ($ofh, "locus_tag", $gSourceId);

	## print product info for non mRNA 
	printProductTbl ($ofh, $tSourceId, $productRef);

      }

      ## print is_pseudo info
      #print $ofh "\t\t\tpseudo\n" if ($tIsPseudo == 1);
      printGenbankTagInfo ($ofh, "pseudo") if ($tIsPseudo == 1);

      ## initial cdsPairs array, codonStart and cdsPairsString for each transcript
      $codonStart = 1;
      $cdsPairsString = "";
      @cdsPairs = ();
    }
  }
  close $ofh;
  print STDERR "done $k\n";
#}
} ## end of foreach sequence

#####################
sub getGeneNames {
  my ($dbhSub, $extDbRlsId) = @_;
  my %geneNameSub;
  my $sqlSub = "
select gf.SOURCE_ID, gfn.NAME from ApiDB.GeneFeatureName gfn, dots.genefeature gf
where gf.NA_FEATURE_ID=gfn.NA_FEATURE_ID and gfn.EXTERNAL_DATABASE_RELEASE_ID=$extDbRlsId
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($sourceId, $name) = split(/\t/, $sqlRefSub->[$i]);
    push (@{$geneNameSub{$sourceId}},$name) if ($sourceId && $name );
    #print STDERR "In getGeneNames, $sourceId, $name##\n" if ($sourceId eq "NBO_1009g0003");
  }
  return \%geneNameSub;
}

sub getGeneSynonyms {
  my ($dbhSub, $extDbRlsId) = @_;
  my %geneSynonymSub;
  my $edName = $organismAbbrev."_dbxref_%_synonym_RSRC";
  my $sqlSub = "
select gf.SOURCE_ID, df.PRIMARY_IDENTIFIER 
from DoTS.DbRefNAFeature dnf, SRes.DbRef df, dots.genefeature gf, SRES.EXTERNALDATABASE ed, SRES.EXTERNALDATABASERELEASE edr 
where gf.NA_FEATURE_ID=dnf.NA_FEATURE_ID and dnf.DB_REF_ID=df.DB_REF_ID and df.EXTERNAL_DATABASE_RELEASE_ID=edr.EXTERNAL_DATABASE_RELEASE_ID 
and edr.EXTERNAL_DATABASE_ID=ed.EXTERNAL_DATABASE_ID and ed.name like '$edName'
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($sourceId, $value) = split(/\t/, $sqlRefSub->[$i]);
    push (@{$geneSynonymSub{$sourceId}}, $value) if ($sourceId && $value);
    #print STDERR "In getGeneNames, $sourceId, $name##\n" if ($sourceId eq "TGME49_217560");
  }
  return \%geneSynonymSub;
}

sub getTransECs {
  my ($dbhSub, $extDbRlsId) = @_;
  my %transECSub;
  my $sqlSub = "
select t.SOURCE_ID, ec.EC_NUMBER from DoTS.AASequenceEnzymeClass aec, SRES.ENZYMECLASS ec, 
DOTS.TRANSLATEDAASEQUENCE tas, DOTS.TRANSLATEDAAFEATURE taf, DOTS.TRANSCRIPT t
where aec.ENZYME_CLASS_ID=ec.ENZYME_CLASS_ID and aec.AA_SEQUENCE_ID=tas.AA_SEQUENCE_ID 
and tas.AA_SEQUENCE_ID=taf.AA_SEQUENCE_ID and taf.NA_FEATURE_ID=t.NA_FEATURE_ID
and t.EXTERNAL_DATABASE_RELEASE_ID=$extDbRlsId
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($sourceId, $value) = split(/\t/, $sqlRefSub->[$i]);
    push (@{$transECSub{$sourceId}}, $value) if ($sourceId && $value);
    #print STDERR "In getTransECs, $sourceId, $value##\n" if ($sourceId eq "NBO_1009g0003");
  }
  return \%transECSub;
}

sub getTransGOs {
  my ($dbhSub, $orgAbbrev, $extDbRlsId) = @_;
  my %transGOSub;

  ## use apidbtuning.GoTermSummary table to get GO info
  my $sqlSub = "
select TRANSCRIPT_SOURCE_ID, GO_ID, GO_TERM_NAME, EVIDENCE_CODE, REFERENCE,
decode(ontology, 'Biological Process', 'GO_process',
                 'Molecular Function', 'GO_function',
                 'Cellular Component', 'GO_component', ontology) 
from webready.GoTermSummary_p
where org_abbrev = '$orgAbbrev'
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($tSourceId, $goId, $goTermName, $evidenceCode, $reference, $ontology) = split(/\t/, $sqlRefSub->[$i]);

    ## remove the prefix GO: and GO_
    $goId =~ s/GO[:|_]//;  ## remove the prefix GO: and GO_

    ## remove the prefix PMID:
    if ($reference =~ /PMID:/i) {
      $reference =~ s/PMID://i;
    } else {
      $reference = "";
    }

    my $value = join ('|', $ontology, $goTermName, $goId, $reference, $evidenceCode);
    push (@{$transGOSub{$tSourceId}}, $value) if ($tSourceId && $value);
  }
  return \%transGOSub;
}

sub getProductNames {
  my ($dbhSub, $extDbRlsId) = @_;
  my %productNameSub;
  my $sqlSub = "
select t.SOURCE_ID, tp.PRODUCT from DOTS.TRANSCRIPT t, ApiDB.TranscriptProduct tp
where t.NA_FEATURE_ID=tp.NA_FEATURE_ID and t.EXTERNAL_DATABASE_RELEASE_ID=$extDbRlsId
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($sourceId, $value) = split(/\t/, $sqlRefSub->[$i]);
    push (@{$productNameSub{$sourceId}}, $value) if ($sourceId && $value);
  }
  return \%productNameSub;
}

sub getTransNotes {
  my ($dbhSub, $extDbRlsId) = @_;
  my %transNoteSub;
  my $sqlSub = "
select t.SOURCE_ID, nfc.COMMENT_STRING from DOTS.NAFEATURECOMMENT nfc, DOTS.TRANSCRIPT t
where t.NA_FEATURE_ID=nfc.NA_FEATURE_ID and t.EXTERNAL_DATABASE_RELEASE_ID=$extDbRlsId
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($sourceId, $value) = split(/\t/, $sqlRefSub->[$i]);
    push (@{$transNoteSub{$sourceId}}, $value) if ($sourceId && $value);
  }
  return \%transNoteSub;
}

sub getGeneAndTransPmids{  ## get pmid in both gene and transcript
  my ($dbhSub, $extDbRlsId) = @_;
  my %genePmidSub;
  my $edName = $organismAbbrev."_dbxref_pmid_%_RSRC";
  my $sqlSub = "
select t.SOURCE_ID, df.PRIMARY_IDENTIFIER
from DoTS.DbRefNAFeature dnf, SRes.DbRef df, DOTS.TRANSCRIPT t, SRES.EXTERNALDATABASE ed, SRES.EXTERNALDATABASERELEASE edr
where t.NA_FEATURE_ID=dnf.NA_FEATURE_ID and dnf.DB_REF_ID=df.DB_REF_ID and df.EXTERNAL_DATABASE_RELEASE_ID=edr.EXTERNAL_DATABASE_RELEASE_ID
and edr.EXTERNAL_DATABASE_ID=ed.EXTERNAL_DATABASE_ID
and ed.name like '$edName'
";
  my $sqlRefSub = readFromDatabase($dbhSub, $sqlSub);
  foreach my $i (0..$#$sqlRefSub) {
    my ($sourceId, $value) = split(/\t/, $sqlRefSub->[$i]);
    push (@{$genePmidSub{$sourceId}}, $value) if ($sourceId && $value);
  }

  my $edName2 = $organismAbbrev."_dbxref_gene2PubmedFromNcbi_RSRC";
  my $sqlSub2 = "
select gf.SOURCE_ID, df.PRIMARY_IDENTIFIER 
from DoTS.DbRefNAFeature dnf, SRes.DbRef df, dots.genefeature gf, SRES.EXTERNALDATABASE ed, SRES.EXTERNALDATABASERELEASE edr 
where gf.NA_FEATURE_ID=dnf.NA_FEATURE_ID and dnf.DB_REF_ID=df.DB_REF_ID and df.EXTERNAL_DATABASE_RELEASE_ID=edr.EXTERNAL_DATABASE_RELEASE_ID 
and edr.EXTERNAL_DATABASE_ID=ed.EXTERNAL_DATABASE_ID 
and ed.name like '$edName2'
";
  my $sqlRefSub2 = readFromDatabase($dbhSub, $sqlSub2);
  foreach my $i (0..$#$sqlRefSub2) {
    my ($sourceId, $value) = split(/\t/, $sqlRefSub2->[$i]);
    push (@{$genePmidSub{$sourceId}}, $value) if ($sourceId && $value);
  }
  return \%genePmidSub;
}

sub printGeneNameTbl {
  my ($fh, $sourceId, $geneNameRef) = @_;
  foreach my $value (@{$geneNameRef->{$sourceId}}) {
    printGenbankTagInfo ($fh, "gene", $value);
  }
  return 0;
}

sub printSynonymTbl {
  my ($fh, $sourceId, $geneSynonymRef) = @_;
  foreach my $value (@{$geneSynonymRef->{$sourceId}}) {
    printGenbankTagInfo ($fh, "synonym", $value);
  }
  return 0;
}

sub printEcTbl {
  my ($fh, $tId, $transECRef) = @_;
  foreach my $value (@{$transECRef->{$tId}}) {
    printGenbankTagInfo ($fh, "EC_number", $value);
  }
  print STDERR "printEcTbl, $tId, $transECRef->{$tId}[0]\n" if ($tId eq "TGME49_226010-t26_1");
  return 0;
}

sub printPmidTbl {
  my ($fh, $id, $tId, $genePmidRef) = @_;
  foreach my $value (@{$genePmidRef->{$id}}) {  ## print these in gene
    printGenbankTagInfo ($fh, "PMID", $value);
  }
  foreach my $value (@{$genePmidRef->{$tId}}) {  ## print these in transcript
    printGenbankTagInfo ($fh, "PMID", $value);
  }
  return 0;
}

sub printGoTbl {
  my ($fh, $tId, $transGORef) = @_;
  my ($tag, $inValue, @values);
  foreach my $value (@{$transGORef->{$tId}}) {
    @values = split (/\|/, $value);
    $tag = shift (@values);
    $inValue = join ('|', @values);
    @values = ();
  }

  printGenbankTagInfo ($fh, $tag, $inValue) if ($tag eq "GO_process" || $tag eq "GO_function" || $tag eq "GO_component" );

  #print STDERR "printGoTbl, $tId, $transGORef->{$tId}[0]\n" if ($tId eq "PBANKA_0100600.1");
  return 0;
}

sub printNoteTbl {
  my ($fh, $tId, $transNoteRef) = @_;
  foreach my $value (@{$transNoteRef->{$tId}}) {
    printGenbankTagInfo ($fh, "Note", $value);
  }
  return 0;
}

sub printDbxRef2EuPathDB {
  my ($fh, $tId, $project) = @_;
  my $inValue;
  if ($project) {
    $inValue = "ApiDB_" . $project . ":" . $tId;
  } else {
    $inValue = "EuPathDB:" . $tId;
  }
  printGenbankTagInfo ($fh, "db_xref", $inValue);
  return 0;
}

sub printProductTbl {
  my ($fh, $tId, $productNameRef) = @_;
  my $productName;

  if ($productNameRef->{$tId}) {
    $productName = join ('/', @{$productNameRef->{$tId}});
  } else {
    $productName = "unspecified product";
  }
  printGenbankTagInfo ($fh, "product", $productName);

  ## if there are more than one product name, print a note with bifunctional
  if ($productNameRef->{$tId} && scalar @{$productNameRef->{$tId}} > 1) {
    printGenbankTagInfo ($fh, "note", "bifunctional");
  }
  return 0;
}

sub printGenbankTagInfo {
  my ($fh, $feature, $value) = @_;
  ($value) ? print $fh "\t\t\t$feature\t$value\n" : print $fh "\t\t\t$feature\n";
  return 0;
}

sub readFromDatabase {
  my ($dbh, $sql) = @_;
  my $stmt = $dbh->prepare($sql);
  $stmt->execute;
  my (@arrays);
  while (my @fetchs = $stmt->fetchrow_array()) {
    my $oneline= $fetchs[0];
    foreach my $i (1..$#fetchs) {
      $oneline .= "\t$fetchs[$i]";
    }
    push @arrays, $oneline;
  }
  $stmt->finish();
  return \@arrays;
}

sub addLocPrefix {
  my ($strand, $start, $end, $prefix5, $prefix3) = @_;
  if ($strand == 1) {
    return "$prefix3".$start, "$prefix5".$end;
  } else {
    return "$prefix5".$start, "$prefix3".$end;
  }
}

sub usage {
  die
"
A script to create genbank feature table file, .tbl
Usage:
  for ISF testing:
          gus4CreateGenbankTableFile --organismAbbrev pvivSal1 --extDbName pvivSaI1_primary_genome_RSRC --extDbRlsVer 2013-05-01
  for genbank submission:
          gus4CreateGenbankTableFile --organismAbbrev pberANKA --extDbName pberANKA_primary_genome_RSRC --extDbRlsVer 2017-01-09
                                     --optionalFunctAnnot 'geneName, synonym, GO, EC, PMID' --project 'PlasmoDB'

NOTE: the output is a set of files ending with .fsa and .tbl,
      So it is better to run it in a subdirectory.
NOTE: the GUS_HOME should point to the instance that the annotation has been loaded

where
  --organismAbbrev: the organism Abbrev in the table apidb.organism
  --extDbName: the external database name for loading genome and annoation
  --extDbRlsVer: the external database release version for loading genome and annotation
  --optionalTaxonId: only required in case the taxonId is not available in ncbi Taxonomy
  --optionalFunctAnnot: comma delimited functional annotation that want to be included in the output file
                        for example, 'geneName, synonym, GO, EC, PMID'
  --project: required for dbxref, for example 'PlasmoDB', 'CryptoDB', 'ToxoDB' ......
";
}
