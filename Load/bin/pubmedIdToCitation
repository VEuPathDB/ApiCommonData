#! /usr/bin/perl

use lib "$ENV{GUS_HOME}/lib/perl";

use LWP::Simple;
use ApiCommonData::Load::XMLUtils qw(extractTag extractTagContent extractAllTags getAttrValue);
use strict;

&usage unless scalar(@ARGV) == 1;
my $pubmedId = $ARGV[0];

my $ncbiEutilsUrl = "http://eutils.ncbi.nlm.nih.gov/entrez/eutils/efetch.fcgi?"
    . "db=pubmed&retmode=xml&rettype=abstract&id=" . $pubmedId;

my $content = LWP::Simple::get ($ncbiEutilsUrl);

die "NCBI did not return a valid '<PubmedArticle>' for pubmed id \"$pubmedId\". Request URL was \"$ncbiEutilsUrl\""
  unless $content =~ /\<PubmedArticle\>/;

#print STDOUT "$content\n";

my $authorList = fetchAuthorList($content);
my $title = fetchTitle($content);
my $publication = fetchPublication($content);

print "$title $authorList $publication\n";
#print "http://www.ncbi.nlm.nih.gov/pubmed/$pubmedId\n";


sub fetchAuthorList {
    my ($content) = @_;

    my @authors;
    my $aContent = extractTagContent ($content, "AuthorList");

    foreach my $author (extractAllTags ($aContent, "Author")) {
	my $attrValue = getAttrValue ($author, "Author", "ValidYN");
	#Some of them don't have this attribute.
	if (!$attrValue || $attrValue eq "Y") {

	    my $lastname = extractTagContent ($author, "(Collective|Last)Name");

	    return "$lastname et al.";
	}
    }
}

sub fetchTitle {
    my ($content) = @_;

    my $title = extractTagContent($content, "ArticleTitle");
    return $title;
}

sub fetchPublication {
    my ($content) = @_;

    my $publication = extractTag ($content, "Journal");
    my ($pubName, $pubVolume, $pubIssue, $pubDate, $pubPages);

    # The name of the journal can come from one of the three sources
    #	1. ISOAbbreviation
    #	2. Title
    #	3. MedlineTA
    # Use the same order of preference in obtaining a name.
    # Don't know what's the difference between Title and MedlineTA, but
    # one of them didn't have Title, but had MedlineTA.

    if (!($pubName = extractTagContent ($publication, "ISOAbbreviation"))) {
	if (!($pubName = extractTagContent ($publication, "Title"))) {
	    $pubName = extractTagContent ($content, "MedlineTA");
	}
    }

    # Publication Date can have three forms:
    #   1. Year and Month, and optionally Day. 
    #   2. Year and Season
    #   3. MedlineDate

    my ($pubYear, $pubSeason, $pubMonth, $pubDay, $pubMedlineDate);
    if ($pubYear = extractTagContent ($publication, "Year")) {
	if ($pubMonth = extractTagContent ($publication, "Month")) {
	    $pubDay = extractTagContent ($publication, "Day");
	    if ($pubDay =~ /^\d\d?$/) {
		$pubDate = "$pubYear $pubMonth $pubDay";
	    } else {
		#$pubDate = "$pubYear $pubMonth";
		$pubDate = "$pubYear"; #BUG in XMLUtils causes malformed XML for $pubMonth
	    }
	} else {
	    $pubDate = "$pubYear "
		. extractTagContent($publication, "Season");
	}
    } else {
	$pubDate = extractTagContent ($publication, "MedlineDate");
    }

    $pubVolume = extractTagContent ($publication, "Volume");
    $pubIssue = extractTagContent ($publication, "Issue");

    # Pagination can have two forms:
    #   1. MedlinePgn - indicates both start and end
    #   2. StartPage, and optionally EndPage
    my $pages = extractTagContent ($content, "Pagination");
    $pubPages = extractTagContent ($pages, "MedlinePgn")
	or $pubPages = extractTagContent ($pages, "StartPage")
	. "-"
	. extractTagContent ($pages, "EndPage");

    return "$pubName $pubDate;$pubVolume($pubIssue):$pubPages";
}

sub usage {
    print STDERR "
usage: pubmedIdToCitation pubmed_id

returns citation
";

    exit(1);
}

