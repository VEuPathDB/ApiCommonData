#!/usr/bin/perl

use strict;
use lib $ENV{GUS_HOME} . "/lib/perl";
use Getopt::Long;
use Data::Dumper;
use DBI;
use JSON;
use XML::LibXML;
use CBIL::Util::PropertySet;

my ($gusConfigFile, $organismAbbrev, $jbrowseConf, $jbrowseFlatFileDir, $jbrowseQueriesXmlFile);

GetOptions("gusConfigFile=s" => \$gusConfigFile,
           "organismAbbrev=s" => \$organismAbbrev,
           "jbrowseConf=s" => \$jbrowseConf,
           "jbrowseFlatFileDir=s" => \$jbrowseFlatFileDir,
           "jbrowseQueriesXmlFile=s" => \$jbrowseQueriesXmlFile)
  or die "Error in command line arguments\n";

unless($gusConfigFile && -e $gusConfigFile) {
  die "gus.config file not provided or not found! \n";
}

unless($organismAbbrev) {
  die "organismAbbrev is required! \n";
}

unless($jbrowseConf) {
  die "jbrowseConf output file is required! \n";
}

unless($jbrowseFlatFileDir && -d $jbrowseFlatFileDir) {
  die "jbrowseFlatFileDir not provided or not a valid directory! \n";
}

unless($jbrowseQueriesXmlFile && -e $jbrowseQueriesXmlFile) {
  die "jbrowseQueriesXmlFile not provided or not found! \n";
}

my @properties = ();
my $gusconfig = CBIL::Util::PropertySet->new($gusConfigFile, \@properties, 1);

my $usr = $gusconfig->{props}->{databaseLogin};
my $pwd = $gusconfig->{props}->{databasePassword};
my $dsn = $gusconfig->{props}->{dbiDsn};

my $dbh = DBI->connect($dsn, $usr, $pwd, {RaiseError => 1}) ||  die "Couldn't connect to database: " . DBI->errstr;

open(FILE, '>', $jbrowseConf) or die "Cannot open file $jbrowseConf for reading: $!";

my $taxonId = &getTaxonId($dbh, $organismAbbrev);

# Check for track files based on XML definitions
my $trackNames = &getTrackNamesFromXml($jbrowseQueriesXmlFile);

# This one is not in the xml.  it is handled separately
push @$trackNames, "unifiedGenomicMassSpec";

my $foundTracks = &findTrackFiles($jbrowseFlatFileDir, $trackNames);

# Output track availability
foreach my $trackName (sort keys %$foundTracks) {
  print FILE "has" . ucfirst($trackName) . "=" . ($foundTracks->{$trackName} ? "1" : "0") ."\n";
}

my $chipChipDatasets = &getChipChipDatasetsHash($dbh, $organismAbbrev);

foreach my $dataset (keys %$chipChipDatasets) {
  print FILE "chipchip::${dataset}::dataset=" . $dataset ."\n";
  foreach my $studyName(keys %{$chipChipDatasets->{$dataset}}){
  	print FILE "chipchip::${dataset}::${studyName}::studyName=" . ${studyName} ."\n";
	foreach my $panName(@{$chipChipDatasets->{$dataset}->{$studyName}}){
		print FILE "chipchip::${dataset}::${studyName}::${panName}::panName=" . $panName ."\n";
	}	
  }
}

close FILE;

sub getTaxonId {
  my ($dbh, $organismAbbrev) = @_;
  my $sql = "select taxon_id from apidb.organism o where o.abbrev = '${organismAbbrev}'";

  my $sh = $dbh->prepare($sql);
  $sh->execute();
  my $result = $sh->fetchrow_array();
  $sh->finish();
  return $result;
}

sub getTrackNamesFromXml {
  my ($xmlFile) = @_;

  my $parser = XML::LibXML->new();
  my $doc = $parser->parse_file($xmlFile);

  my @trackNames;
  foreach my $track ($doc->findnodes('//track')) {
    my $name = $track->getAttribute('name');
    push @trackNames, $name if $name;
  }

  return \@trackNames;
}

sub findTrackFiles {
  my ($flatFileDir, $trackNames) = @_;

  my %foundTracks;

  foreach my $trackName (@$trackNames) {
    # Look for files that start with the track name
    opendir(my $dh, $flatFileDir) or die "Cannot open directory $flatFileDir: $!";
    my @files = grep { /^${trackName}/ } readdir($dh);
    closedir($dh);

    $foundTracks{$trackName} = scalar(@files) > 0 ? 1 : 0;
  }

  return \%foundTracks;
}


sub getChipChipDatasetsHash {
  my ($dbh, $organismAbbrev) = @_;

  my %chipChipDatasets;

 my $sql = "select d.name, s.name, pan.name
from study.nodeset s
   , SRES.EXTERNALDATABASERELEASE r
   , SRES.EXTERNALDATABASE d
   , study.protocolappnode pan
   , study.nodenodeset sl
where d.name like '${organismAbbrev}%_chipChipExper_%'
and s.EXTERNAL_DATABASE_RELEASE_ID = r.EXTERNAL_DATABASE_RELEASE_ID
and r.EXTERNAL_DATABASE_ID = d.EXTERNAL_DATABASE_ID
and s.node_set_id = sl.node_set_id
and sl.protocol_app_node_id = pan.PROTOCOL_APP_NODE_ID
";

  my $sh = $dbh->prepare($sql);
  $sh->execute();

  while(my ($dataset, $study, $panName, $panId) = $sh->fetchrow_array()){
    push @{$chipChipDatasets{$dataset}->{$study}},$panName ;
  }
  $sh->finish();
  return \%chipChipDatasets;
}

1;
